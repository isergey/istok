
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Список.Параметры.УстановитьЗначениеПараметра("ОбъектСистемы", Справочники.ОбъектыСистемы.ПустаяСсылка());
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	Элементы.СписокКомпоновщикНастроекПользовательскиеНастройки.Видимость = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектСистемыПриИзменении(Элемент)
	ОбъектСистемыПриИзмененииСервер();
	
	Для Каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		Элементы.Список.Развернуть(ВыделеннаяСтрока, Истина);
	КонецЦикла;
	
	Если ОбъектСистемы.Пустая() Тогда
		АвтоЗаголовок = Истина;
		Заголовок = "";
	Иначе
		АвтоЗаголовок = Ложь;
		Заголовок = Строка(ОбъектСистемы);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбъектСистемыПриИзмененииСервер()
	Список.Параметры.УстановитьЗначениеПараметра("ОбъектСистемы", ОбъектСистемы);
	Список.ПараметрыДанных.ОбъектСистемы.Значение = ОбъектСистемы;
	
	Для Каждого ДобавленнаяКолонка Из ДобавленныеКолонки Цикл
		Элементы.Удалить(Элементы.Найти(ДобавленнаяКолонка.Значение));
	КонецЦикла;
	
	ДобавленныеКолонки.Очистить();
	
	Список.КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.Полное);
	Элементы.СписокКомпоновщикНастроекПользовательскиеНастройки.Видимость = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектСистемы", ОбъектСистемы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбъектыСистемыКолонки.Колонка.Наименование КАК Наименование,
	|	ОбъектыСистемыКолонки.Колонка.СистемноеНаименование КАК СистемноеНаименование
	|ИЗ
	|	Справочник.ОбъектыСистемы.Колонки КАК ОбъектыСистемыКолонки
	|ГДЕ
	|	ОбъектыСистемыКолонки.Ссылка = &ОбъектСистемы
	|	И ОбъектыСистемыКолонки.ВыводитьВСписке";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Результат = Результат.Выбрать();
		
		Пока Результат.Следующий() Цикл
			ИмяНовогоЭлемента = СтрШаблон("Список%1", Результат.СистемноеНаименование);
			НовыйЭлемент = Элементы.Добавить(ИмяНовогоЭлемента, Тип("ПолеФормы"), Элементы.Список);
			НовыйЭлемент.Заголовок = СтрЗаменить(Результат.Наименование, " _", "");
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.ПутьКДанным = СтрШаблон("Список.Ссылка.[%1]", Результат.Наименование);
			НовыйЭлемент.Ширина = 1;
			НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
			
			ДобавленныеКолонки.Добавить(ИмяНовогоЭлемента);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
